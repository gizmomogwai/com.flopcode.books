buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.5.0'
  }
}

apply plugin: 'com.android.application'

repositories {
  jcenter()
}

android {
  defaultConfig {
    versionCode 16
    versionName "16.0"
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:23.2.0'
  compile 'com.android.support:design:23.2.0'
  compile 'com.google.code.gson:gson:2.6.2'
  compile 'com.google.guava:guava:19.0'
  compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
  compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
  compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
  compile 'com.jakewharton:butterknife:7.0.1'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-core:1.3'
  testCompile 'org.assertj:assertj-core:1.7.0'
}

// from http://stackoverflow.com/a/22597752
def passwordFromKeychain = { name ->
  def proc = ["security", "find-generic-password", "-s", name, "-a", name, "-w"].execute()
  proc.waitFor()
  def stdout = proc.in.text
  return stdout.trim()
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 23
  }

  signingConfigs {
    release {
      storeFile file("release.keystore")
      storePassword passwordFromKeychain("books-play-store")
      keyAlias "books"
      keyPassword passwordFromKeychain("books-play-store")
    }
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }
}
